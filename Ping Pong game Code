
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>

#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 64

// OLED I2C pins (use default A4=SDA, A5=SCL)
#define OLED_ADDR 0x3C
#define OLED_RESET -1

Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);

// Joystick and buttons
const int SW_pin = 4;        // Joystick switch (digital)
const int player1 = A0;      // Analog Y of player 1
const int player2 = A1;      // Analog Y of player 2
const int RESET_BUTTON = 7;  // Optional reset button
const int BEEPER = 8;        // Buzzer pin

// Game constants
const unsigned long PADDLE_RATE = 45;
const unsigned long BALL_RATE = 5;
const uint8_t PADDLE_HEIGHT = 12;

int player1Score = 0;
int player2Score = 0;
int maxScore = 8;
bool resetBall = false;

uint8_t ball_x = 64, ball_y = 32;
int8_t ball_dir_x = 1, ball_dir_y = 1;
unsigned long ball_update;
unsigned long paddle_update;

const uint8_t PLAYER2_X = 22;
uint8_t player2_y = 26;

const uint8_t PLAYER1_X = 105;
uint8_t player1_y = 26;

void drawCourt();
void drawScore();
void gameOver();
void soundBounce();
void soundPoint();

void setup() {
  Serial.begin(9600);
  if (!display.begin(SSD1306_SWITCHCAPVCC, OLED_ADDR)) {
    Serial.println("SSD1306 init failed!");
    while (true);
  }

  pinMode(BEEPER, OUTPUT);
  pinMode(SW_pin, INPUT_PULLUP);
  pinMode(RESET_BUTTON, INPUT_PULLUP);

  display.clearDisplay();
  drawCourt();
  drawScore();
  display.display();

  ball_update = millis();
  paddle_update = ball_update;
}

void loop() {
  bool update = false;
  unsigned long time = millis();

  if (resetBall) {
    if (player1Score == maxScore || player2Score == maxScore) {
      gameOver();
    } else {
      display.clearDisplay();
      drawScore();
      drawCourt();
      ball_x = random(45, 50);
      ball_y = random(23, 33);

      do { ball_dir_x = random(-1, 2); } while (ball_dir_x == 0);
      do { ball_dir_y = random(-1, 2); } while (ball_dir_y == 0);

      resetBall = false;
    }
  }

  // Move ball
  if (time > ball_update) {
    uint8_t new_x = ball_x + ball_dir_x;
    uint8_t new_y = ball_y + ball_dir_y;

    if (new_x == 0 || new_x == 127) {
      if (new_x == 0) {
        player1Score++;
        display.clearDisplay();
        soundPoint();
        resetBall = true;
      } else {
        player2Score++;
        display.clearDisplay();
        soundPoint();
        resetBall = true;
      }
      ball_dir_x = -ball_dir_x;
      new_x += ball_dir_x;
    }

    if (new_y == 0 || new_y == 63) {
      soundBounce();
      ball_dir_y = -ball_dir_y;
      new_y += ball_dir_y;
    }

    // Player 2 paddle
    if (new_x == PLAYER2_X && new_y >= player2_y && new_y <= player2_y + PADDLE_HEIGHT) {
      soundBounce();
      ball_dir_x = -ball_dir_x;
      new_x += ball_dir_x;
    }

    // Player 1 paddle
    if (new_x == PLAYER1_X && new_y >= player1_y && new_y <= player1_y + PADDLE_HEIGHT) {
      soundBounce();
      ball_dir_x = -ball_dir_x;
      new_x += ball_dir_x;
    }

    display.drawPixel(ball_x, ball_y, BLACK);
    display.drawPixel(new_x, new_y, WHITE);
    ball_x = new_x;
    ball_y = new_y;

    ball_update = time + BALL_RATE;
    update = true;
  }

  // Move paddles
  if (time > paddle_update) {
    paddle_update = time + PADDLE_RATE;

    // Player 2
    display.drawFastVLine(PLAYER2_X, player2_y, PADDLE_HEIGHT, BLACK);
    if (analogRead(player2) < 300) player2_y -= 1;
    if (analogRead(player2) > 700) player2_y += 1;
    player2_y = constrain(player2_y, 1, 63 - PADDLE_HEIGHT);
    display.drawFastVLine(PLAYER2_X, player2_y, PADDLE_HEIGHT, WHITE);

    // Player 1
    display.drawFastVLine(PLAYER1_X, player1_y, PADDLE_HEIGHT, BLACK);
    if (analogRead(player1) < 300) player1_y -= 1;
    if (analogRead(player1) > 700) player1_y += 1;
    player1_y = constrain(player1_y, 1, 63 - PADDLE_HEIGHT);
    display.drawFastVLine(PLAYER1_X, player1_y, PADDLE_HEIGHT, WHITE);
  }

  update = true;

  if (update) {
    drawScore();
    display.display();

    if (digitalRead(SW_pin) == LOW) { // Press joystick to end game
      gameOver();
    }
  }
}

void drawCourt() {
  display.drawRect(0, 0, SCREEN_WIDTH, SCREEN_HEIGHT, WHITE);
}

void drawScore() {
  display.setTextSize(2);
  display.setTextColor(WHITE);
  display.setCursor(45, 0);
  display.println(player2Score);
  display.setCursor(75, 0);
  display.println(player1Score);
}

void gameOver() {
  display.clearDisplay();
  display.setTextSize(2);
  display.setTextColor(WHITE);
  if (player1Score > player2Score) {
    display.setCursor(20, 15);
    display.print("Player 1");
    display.setCursor(40, 35);
    display.print("Won");
  } else {
    display.setCursor(20, 15);
    display.print("Player 2");
    display.setCursor(40, 35);
    display.print("Won");
  }
  display.display();
  delay(2000);
  player1Score = 0;
  player2Score = 0;
  resetBall = true;
}

void soundBounce() {
  tone(BEEPER, 500, 50);
}

void soundPoint() {
  tone(BEEPER, 100, 50);
}
